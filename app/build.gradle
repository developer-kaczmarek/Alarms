plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
}

android {
    compileSdk target_sdk_version

    defaultConfig {
        applicationId "io.github.kaczmarek.alarms"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
        freeCompilerArgs += "-Xopt-in=kotlin.ExperimentalStdlibApi"
        freeCompilerArgs += "-Xopt-in=kotlin.time.ExperimentalTime"
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    sourceSets {
        main.kotlin.srcDirs += "src/main/kotlin"
        debug.kotlin.srcDirs += "src/debug/kotlin"
        androidTest.kotlin.srcDirs += "src/androidTest/kotlin"
        test.kotlin.srcDirs += "src/test/kotlin"
    }
}

dependencies {
    coreLibraryDesugaring libs.android.desugar

    // Kotlin
    implementation libs.coroutines.core
    implementation libs.coroutines.android

    // UI
    implementation libs.compose.ui
    implementation libs.compose.material
    implementation libs.compose.preview
    implementation libs.activity.compose
    implementation libs.appcompat

    // DI
    implementation libs.koin.android

    // Serialization
    implementation libs.serialization.json

    // Architecture
    implementation libs.bundles.sesame
    implementation libs.bundles.decompose

    // Debugging
    implementation libs.timber

    // Work Manager
    implementation libs.workManager
}